plugins {
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "com.diffplug.spotless" version "6.11.0"
	id 'java'
}

group = 'dev.findfirst'
version = '1.0.0'
sourceCompatibility = '17'
compileJava.dependsOn 'spotlessApply'

spotless {
	// optional: limit format enforcement to just the files changed by this feature branch
	ratchetFrom 'origin/main'

	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'
		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		toggleOffOn()
		// optional: you can specify import groups directly
		// note: you can use an empty string for all the imports you didn't specify explicitly, and '\\#` prefix for static imports
		importOrder('java', 'jakarta')
		// optional: instead of specifying import groups directly you can specify a config file
		// export config file: https://github.com/diffplug/spotless/blob/main/ECLIPSE_SCREENSHOTS.md#creating-spotlessimportorder

		removeUnusedImports()

		// Choose one of these formatters.
		googleJavaFormat()   // has its own section below

		formatAnnotations()  // fixes formatting of type annotations, see below

	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework:spring-websocket:6.0.13'
	implementation 'org.springframework:spring-messaging:6.0.13'
	implementation 'org.springframework.security:spring-security-config:6.0.1'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-mail:3.0.1'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
	implementation 'jakarta.json:jakarta.json-api:2.0.1'
	implementation 'co.elastic.clients:elasticsearch-java:8.10.2'
	implementation group: 'org.springframework.data', name: 'spring-data-elasticsearch', version: '5.1.4'
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

	compileOnly 'org.springframework.boot:spring-boot-starter-webflux'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test:6.0.1'

	testImplementation 'io.projectreactor:reactor-test'

	developmentOnly("org.springframework.boot:spring-boot-devtools")
	// developmentOnly("org.springframework.boot:spring-boot-docker-compose")
}

test {
	useJUnitPlatform()
	systemProperty 'spring.profiles.active', 'dev'
	// testLogging.showStandardStreams = true


	// Show test results.
	testLogging {
		events "passed", "skipped", "failed"
	}
}
